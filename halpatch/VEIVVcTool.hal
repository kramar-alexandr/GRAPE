external function Integer APIInstallNr(LongInt,Date);
external function Boolean CurncyCodeRegistered(string);
external function Integer GetVATLaw();
external procedure GetAPAcc(string,var string);
external function Date FindDueDateInAccPeriod(Date);
external procedure CalcCashDiscount(var record VIVc);
external function LongInt NextFreeCashSerNr(row PMBlock,Boolean);
external function Boolean GetPD(var record PDVc);
external function LongInt DateDiff(Date,Date);
external function val MulRateToBase1(var string,val,val,val,val,val,val,roundmode);
external procedure VIVc_PastePayDeal(var record VIVc);
external function val FindVAT(string,val,Integer,Integer);
external procedure VICalcVals(var record VIVc);
external function Boolean GetAccName(string,var string,Integer);
external procedure VISumup(record VIVc,var val);
external procedure VIGetRate(var record VIVc);
external procedure VICalcEReb(record VIVc);
external function Boolean PasteVendInVI(record VIVc,var string,Boolean);
external function LongInt GetCurUserLastNr(string);
external procedure PUCalcPerc(val,string,var val);
external function Boolean IsVATCodeDefined(string);

procedure AddVEIVRowToVI(Boolean GroupByAcc,string VECostAcc,string VEIVCostAcc,Integer InclVAT,row VEIVVc VEIVrw,var record VIVc VIp)
BEGIN
  row VIVc VIrw;
  Integer i,rwcnt;
  string 10 costacc;
  Boolean rowexistsf;
  string 255 tstr;
  val v;

  costacc = VEIVrw.CostAcc;
  if (blank(costacc)) then begin
    costacc = VEIVCostAcc;
  end;
  if (blank(costacc)) then begin
    costacc = VECostAcc;
  end;
  rwcnt = MatRowCnt(VIp);
  if (GroupByAcc) then begin
    for (i=0;i<rwcnt;i=i+1) begin
      MatRowGet(VIp,i,VIrw);
      if (costacc==VIrw.AccNumber) and (VIrw.VATCode==VEIVrw.VATCode) and ((VIrw.Item==VEIVrw.ArtCode) or blank(VEIVrw.ArtCode)) then begin
        rowexistsf = true;
        if (InclVAT!=0) then begin    
          v = VEIVrw.VESum - FindVAT(VEIVrw.VATCode,VEIVrw.VESum,1,0);       
        end else begin
          v = VEIVrw.VESum;
        end;
        VIrw.Sum = VIrw.Sum + v;
        VIrw.qty = VIrw.qty + VEIVrw.Quant;
        MatRowPut(VIp,i,VIrw);
        i = rwcnt;
      end;
    end;
  end;
  if (rowexistsf==false) then begin
    ClearRow(VIp,VIrw,1);
    VIrw.stp = 1;
    VIrw.AccNumber = costacc;
    if (GetAccName(VIrw.AccNumber,tstr,60)) then begin end;    
    if (nonblank(VEIVrw.ArtCode)) then begin
      VIrw.Comment = VEIVrw.VESpec;    
    end;
    if (blank(VIrw.Comment) or GroupByAcc) then begin
      VIrw.Comment = tstr;    
    end;
    VIrw.VATCode = VEIVrw.VATCode;
    VIrw.TaxTemplateCode = VEIVrw.TaxTemplateCode;
    if (InclVAT!=0) then begin    
      VIrw.Sum = VEIVrw.VESum - FindVAT(VEIVrw.VATCode,VEIVrw.VESum,1,0);
    end else begin
      VIrw.Sum = VEIVrw.VESum;
    end;
    VIrw.Objects = VEIVrw.Objects;
    VIrw.qty = VEIVrw.Quant;
    VIrw.Item = VEIVrw.ArtCode;
    if (VIrw.stp==6) then begin
      VIrw.PrepayNr = -1;
    end;    
    VIrw.OrdRow = VEIVrw.VEOrdRow;
    if (VIrw.OrdRow<0) then begin
      VIrw.OrdRow = -2;
    end;
    MatRowPut(VIp,rwcnt,VIrw);
  end;
  RETURN;
END;

global
updating procedure VICalcInstallment(record VIVc VIp,Date tdp,val sump,string instcode,var LongInt pdaysp,Boolean createsupp)
BEGIN
  record InstallmentVc Intr;
  row InstallmentVc Insrw;
  record APInstallVc APIr;
  record APInstallVc oldAPIr;
  Integer rwcnt,i;
  date tdr;
  val sum,t;
  Boolean found,delf;
  
  tdr = tdp;
  pdaysp = 0;
  if (sump==0) then begin
    delf = true;
  end else begin
    delf = false;
  end;
  Intr.Code = instcode;
  if (ReadFirstMain(Intr,1,true)) then begin
    rwcnt = MatRowCnt(Intr);
    for (i=0;i<rwcnt;i=i+1) begin
      MatRowGet(Intr,i,Insrw);
      if (Insrw.Months!=-1) then begin
       tdr = AddMonth(tdr,Insrw.Months);
//       tdr.day = 1;// You can control this from the rest of the Payment term fields 
      end;
      if (Insrw.Days!=-1) then begin
        tdr = AddDay(tdr,Insrw.Days);
      end;
      if (createsupp) then begin          
        APIr.VISerNr = VIp.SerNr;
        APIr.DueDate = tdr;
        found = ReadFirstKey("VISerNr",APIr,2,true);
        RecordCopy(oldAPIr,APIr);
        if (found) then begin
        end else begin
          APIr.VISerNr = VIp.SerNr;
          APIr.DueDate = tdr;
          APIr.VECode = VIp.VECode;
          APIr.CurncyCode = VIp.CurncyCode;
          APIr.RVal = 0;
          APIr.BookRVal = 0;
        end;
        PUCalcPerc(sump,Insrw.Prc,t);
        APIr.RVal = t;
        sum = MulRateToBase1(VIp.CurncyCode,sump,VIp.FrRate,VIp.ToRateB1,VIp.ToRateB2,VIp.BaseRate1,VIp.BaseRate2,DefaultCurRoundOff);
        PUCalcPerc(sum,Insrw.Prc,t);
        APIr.BookRVal = t;
        if (delf==false) then begin
          if (found) then begin
            if (RecordUpdate(oldAPIr,APIr,found)==0) then begin
            end;
          end else begin
            APIr.InstallNr = APIInstallNr(APIr.VISerNr,APIr.DueDate);
            if (RecordStore(APIr,true)) then begin
            end;
          end;
        end else begin
          if (found==true) then begin
            RecordDelete(oldAPIr);
          end;
        end;
      end;
    end;
    if (createsupp==false) then begin
      pdaysp = DateDiff(tdr,tdp);
    end;
  end;
  RETURN;
END;

procedure VICalcInstallment2(record VIVc VIp,Date tdp,val sump,string instcode,var LongInt pdaysp)
begin
  record InstallmentVc Intr;
  row InstallmentVc Insrw;
  Integer rwcnt,i;
  date tdr;
  val sum,t;
  Boolean found,delf;
  record VIVc origVIr;
  string 20 linstcode;
  record PDVc PDr;
  
  linstcode = instcode;
  if (VIp.InvType==kInvoiceTypeCredit or VIp.InvType==kInvoiceTypeCreditSpecialSales) then begin
    origVIr.SerNr = VIp.CredInv;
    if (ReadFirstMain(origVIr,1,true)) then begin 
      PDr.Code = origVIr.PayDeal;
      if (GetPD(PDr)) then begin    
        linstcode = PDr.Installment;
      end;
    end;
  end;
  tdr = tdp;
  pdaysp = 0;
  if (sump==0) then begin
    delf = true;
  end else begin
    delf = false;
  end;
  Intr.Code = linstcode;
  if (ReadFirstMain(Intr,1,true)) then begin
    rwcnt = MatRowCnt(Intr);
    for (i=0;i<rwcnt;i=i+1) begin
      MatRowGet(Intr,i,Insrw);
      if (Insrw.Months!=-1) then begin
       tdr = AddMonth(tdr,Insrw.Months);
//       tdr.day = 1;// You can control this from the rest of the Payment term fields 
      end;
      if (Insrw.Days!=-1) then begin
        tdr = AddDay(tdr,Insrw.Days);
      end;      
    end;
    pdaysp = DateDiff(tdr,tdp);
  end;
  return;
end;

global
procedure VICalcEReb(record VIVc VIp)
BEGIN
  record PDVc PDr;
  record PMBlock PMRec;
  row PMBlock PMrw;
  LongInt pdays;
  Date td,blankd;
  LongInt restdays;
  Integer i,mrwcnt;
  record SRBlock SRRec;
  LongInt newnr;
  record MainCLBlock MainCLRec;
  string 255 tstr;
  record VIVc orgVIr;
    
  PDr.Code = VIp.PayDeal;
  if (!GetPD(PDr)) then begin

    BlockLoad(MainCLRec);
    BlockLoad(PMRec);
    mrwcnt = MatRowCnt(PMRec);
    for (i = 0; i<mrwcnt; i=i+1) begin
      MatRowGet(PMRec,i,PMrw);
      if (PMrw.Code==VIp.PayDeal) then begin
        VIp.DueDate = VIp.InvDate;
        VIp.DueDate = VIp.EndJobDate;// Edit ************************** Monday, 23 November 2015 17:26:57
        VIp.InvType = kInvoiceTypeCash;
        VIp.RebVal = blankval;
        VIp.RebVal2 = blankval;
        if (PMrw.TSerStartOut!=-1) then begin
          if (MainCLRec.CommonNoSer!=0) then begin
            VIp.SerNr = NextFreeCashSerNr(PMrw,false);
          end;
        end;
        VIp.APAcc = PMrw.AccNr;
        VIp.CredMark = "";
        goto LVICalcEReb;
      end;
    end;
    VIp.PayDeal = "";
    VIp.CredMark = "";
    goto LVICalcEReb;
  end;
  pdays = PDr.pdays;
  VIp.InvType = PDr.PDType;
  VIp.pdays = PDr.pdays;
  if (VIp.InvType==0) then begin VIp.InvType = kInvoiceTypeNormal; end;
  VIp.RebVal = blankval;
  VIp.RebVal2 = blankval;
  if (PDr.pdvrebt!=0) or (PDr.pdvrebt2!=0) then begin
    CalcCashDiscount(VIp);
  end;
  VIp.RebDate = blankd;
  VIp.RebDate2 = blankd;
  if (nonblankdate(VIp.InvDate)) then begin
    if (PDr.pdvrebt!=blankval) then begin
      if (PDr.pdrdays!=-1) then begin
        VIp.RebDate = AddDay(VIp.InvDate,PDr.pdrdays);
      end;
    end;
    if (PDr.pdvrebt2!=blankval) then begin
      if (PDr.pdrdays2!=-1) then begin
        VIp.RebDate2 = AddDay(VIp.InvDate,PDr.pdrdays2);
      end;
    end;
  end;
  VIp.DueDate = VIp.InvDate;
  VIp.DueDate = VIp.EndJobDate;// Edit ************************** Monday, 23 November 2015 17:26:57
  if (pdays!=-1) then begin
    VIp.DueDate = AddDay(VIp.InvDate,pdays);
    VIp.DueDate = AddDay(VIp.EndJobDate,pdays);
  end;
  switch (VIp.InvType) begin
    case kInvoiceTypeInterest:
      VIp.InvType = 1;
      td = VIp.InvDate;
      td = AddMonth(td,1);
      if (PDr.pdays==0) then begin
        td = AddMonth(td,1);
      end;
      td = AddDay(td,-GetDay(td)+1);
      restdays = DateDiff(td,VIp.InvDate);
      if (PDr.pdays>=0) then begin
        restdays = restdays - 1;
      end;
      pdays = PDr.pdays + restdays;
      VIp.DueDate = AddDay(VIp.InvDate,pdays);
      VIp.DueDate = AddDay(VIp.EndJobDate,pdays);// Edit ************************** Monday, 23 November 2015 17:31:18
      VIp.CredMark = "";
    case kInvoiceTypeDownpayment:
      VIp.InvType = 1;
      //VIp.DueDate = FindDueDateInAccPeriod(VIp.InvDate);// Edit ************************** Monday, 23 November 2015 17:31:36
      VIp.DueDate = FindDueDateInAccPeriod(VIp.EndJobDate);// Edit ************************** Monday, 23 November 2015 17:31:40
      if (PDr.pdays>0) then begin
        VIp.DueDate = AddDay(VIp.DueDate,PDr.pdays);
      end;
      VIp.CredMark = "";
    case kInvoiceTypeCashInvoiceReceiptPRT:
      VIp.APAcc = PDr.pdCashAcc;
      VIp.CredMark = "";
    case kInvoiceTypeCash:
      VIp.APAcc = PDr.pdCashAcc;
      VIp.CredMark = "";
    case kInvoiceTypeCreditSpecialSales: goto LkInvoiceTypeCredit;
    case kInvoiceTypeCredit:
      LkInvoiceTypeCredit:;
//      if (VIp.CredInv<=0) then begin
//        VIp.APAcc = "";
//      end;
      VIp.CredMark = USetStr(1078);
      if (VIp.SerNr<=0) then begin
        newnr = GetCurUserLastNr("CreditVIVc");
        if (newnr==-1) then begin
          BlockLoad(SRRec);
          newnr = SRRec.LastCredVINr;
        end;
        if (newnr>0) then begin
          VIp.SerNr = NextSerNr("VIVc",VIp.TransDate,newnr,false,VIp.LangCode);
        end;
       end;
    otherwise
      VIp.CredMark = "";
      GetAPAcc(VIp.VECode,tstr);
      VIp.APAcc = tstr;
      if (VIp.SerNr<=0) and SingleUserMode then begin
        newnr = GetCurUserLastNr("VIVc");
        if (newnr==-1) then begin
          BlockLoad(SRRec);
          newnr = SRRec.LastVINr;
        end;
        if (newnr>0) then begin
          VIp.SerNr = NextSerNr("VIVc",VIp.TransDate,newnr,false,VIp.LangCode);
        end;
      end;
  end;
  VIp.InstallmentInv = 0;
  if (nonblank(PDr.Installment)) then begin
    if ((VIp.InvType==kInvoiceTypeNormal or VIp.InvType==kInvoiceTypeNormalSpecialSales) or (VIp.InvType==kInvoiceTypeInterest)) then begin
      VICalcInstallment2(VIp,VIp.DueDate,VIp.PayVal,PDr.Installment,restdays);
      VIp.InstallmentInv = 1;
      if (restdays!=-1) then begin
        VIp.DueDate = AddDay(VIp.DueDate,restdays);
      end;
    end;
  end;
LVICalcEReb:;
	
	VIp.DueDate = AddDay(VIp.DueDate,2);// Edit ************************** Monday, 23 November 2015 17:49:14
	
  RETURN;
END;

global
procedure VIVc_PasteEndJobDate(var record VIVc VIp)
BEGIN
  
  VICalcEReb(VIp); 

  RETURN;
END;

updating function Integer PasteVEIVInVI(record VEIVVc VEIVr,var record VIVc VIp)
BEGIN
  record EInvoiceBlock EInvoiceRec;
  Integer res;
  record CUVc VEr;
  Boolean testf,GroupByAcc,manycostaccf;
  record VITBlock VITRec;
  string 255 tstr;
  row VEIVVc VEIVrw;
  Integer i,rwcnt,trwcnt;
  row VIVc VIrw;
  val t;
  
  BlockLoad(EInvoiceRec);
  GroupByAcc = EInvoiceRec.VEIVRowTransfer==0;
  res = 1434;
  if (nonblank(VEIVr.OurOrdNr)) then begin
//    goto LPasteVEIVInVI;
  end;
  VIp.TransDate = VEIVr.TransDate;
  VIp.InvDate = VEIVr.InvDate;
  VIp.DueDate = VEIVr.PayDate;
  VIp.InvoiceNr = VEIVr.InvoiceNr;
  if (blank(VIp.InvoiceNr)) then begin
    VIp.InvoiceNr = VEIVr.VESerNr;
  end;
  VIp.Comment = VEIVr.InvComment;
  VIp.POSerNr = VEIVr.OurOrdNr;
  VIp.CredInv = VEIVr.CredInv;
  if (blank(VEIVr.VECode)) then begin
    VEr.VATNr = VEIVr.VEVATNr;
    testf = ReadFirstKey("VATNr",VEr,1,true);
  end else begin
    VEr.Code = VEIVr.VECode;
    testf = ReadFirstMain(VEr,1,true);
  end;
  if (VEr.blockedFlag!=0) then begin
    testf = false;
  end;
  if (testf==false) then begin
    res = 1434;
    goto LPasteVEIVInVI;
  end;
//  BlockLoad(VITRec);
//  if (VITRec.TransferItems!=2) then begin 
//    if (blank(VEr.AccCost)) then begin 
//      res = 1434;  
//      goto LPasteVEIVInVI; 
//    end;
//  end;
  VIp.VECode = VEr.Code;
  if (PasteVendInVI(VIp,tstr,false)) then begin
    if (nonblank(VEIVr.OurPayDeal)) then begin
      VIp.PayDeal = VEIVr.OurPayDeal;
      VIVc_PastePayDeal(VIp);
    end;
    VIp.CurncyCode = VEIVr.CurncyCode;
    if (CurncyCodeRegistered(VIp.CurncyCode)==false) then begin
      res = 1260;  
      goto LPasteVEIVInVI; 
    end;
    VICalcEReb(VIp);
  end else begin
    res = 1434;  
    goto LPasteVEIVInVI; 
  end;
  VIp.RefStr = VEIVr.CalcFinRef;
  rwcnt = MatRowCnt(VEIVr);
  for (i=0;i<rwcnt;i=i+1) begin
    MatRowGet(VEIVr,i,VEIVrw);
    testf = true;
    if (VEIVrw.stp==3) then begin testf = false; end; 
    if (VEIVrw.stp==4) then begin testf = false; end; 
    if (VEIVrw.stp==5) then begin testf = false; end; 
    if (VEIVrw.stp==7) then begin testf = false; end; 
    if (VEIVrw.stp==8) then begin testf = false; end; 
    if (VEIVrw.stp==9) then begin testf = false; end; 
    if (VEIVrw.stp==101) then begin testf = false; end; 
    if (VEIVrw.VESum==blankval) then begin testf = false; end;
//      if (blank(VEIVrw.ArtCode)) then begin testf = false; end;
    if (testf) then begin
      trwcnt = MatRowCnt(VIp);
      AddVEIVRowToVI(GroupByAcc,VEr.AccCost,VEIVr.CostAcc,VEIVr.InclVAT,VEIVrw,VIp);
      if (trwcnt!=MatRowCnt(VIp) and MatRowCnt(VIp)>1) then begin manycostaccf = true; end;
    end;
  end;
  for (i=0;i<rwcnt;i=i+1) begin
    MatRowGet(VIp,i,VIrw);
    VIp.PayVal = VIp.PayVal + VIrw.Sum;
  end;
  if (manycostaccf==false) then begin
    MatRowDelete(VIp,0);
    VEIVrw.VESum = VEIVr.Sum1;
    AddVEIVRowToVI(GroupByAcc,VEr.AccCost,VEIVr.CostAcc,VEIVr.InclVAT,VEIVrw,VIp);
    VIp.PayVal = VEIVr.Sum4;
    VIp.VATVal = VEIVr.Sum3;
  end;
  VIGetRate(VIp);
  VICalcVals(VIp);
  VISumup(VIp,t);
  if (manycostaccf) then begin
    if (VIp.CalcVATVal==0) then begin
      for (i=0;i<rwcnt;i=i+1) begin
        MatRowGet(VEIVr,i,VEIVrw);
        if (VEIVrw.stp==101) then begin 
          VIp.PayVal = VIp.PayVal + VEIVrw.VESum;
        end; 
      end;
    end else begin
      VIp.PayVal = VIp.PayVal + VIp.CalcVATVal;
    end;
  end;
  res = 0;
//  VIp.PayVal = VEIVr.Sum4;
LPasteVEIVInVI:;  
  PasteVEIVInVI = res;
  RETURN;    
END;

global
function LongInt ValidateVEIV(record VEIVVc VEIVr,var string fieldname,var Integer rownr)
begin
  LongInt res;
  row VEIVVc VEIVrw;
  Integer i,rwcnt;
  record EInvoiceBlock EIb;

  res = 0;
  BlockLoad(EIb);
  rwcnt = MatRowCnt(VEIVr);
  for (i=0;i<rwcnt;i=i+1) begin
    MatRowGet(VEIVr,i,VEIVrw);
    if (VEIVrw.stp==1 and VEIVrw.VESum!=blankval) then begin
      if (EIb.NoVATOnVEIVVcRow==0 and !IsVATCodeDefined(VEIVrw.VATCode)) then begin
        res = 20809;
        fieldname = "VATCode";
        rownr = i;
        goto LValidateVEIV;
      end;
    end;
  end;
  
LValidateVEIV:;
  ValidateVEIV = res;
  return;
end;

global
updating function Integer CreateVIFromVEIV(LongInt veivnr,var record VIVc VIp)
BEGIN
  Integer res;
  record VEIVVc VEIVr,OldVEIVr;
  record POVc POr;
  record SRBlock SRRec;
  LongInt newnr;

  VEIVr.SerNr = veivnr;
  if (ReadFirstMain(VEIVr,1,true)) then begin end;
  if (VEIVr.OKStatus!=1) then begin
    res = 20803;
    goto LCreateVIFromVEIV;
  end;
  if (VEIVr.VISerNr!=-1) then begin
    res = 20808;
    goto LCreateVIFromVEIV;
  end;
  RecordNew(VIp);
  res = PasteVEIVInVI(VEIVr,VIp);
  if (res!=0) then begin
    goto LCreateVIFromVEIV;
  end;  
  if (VIp.SerNr==-1) then begin
    BlockLoad(SRRec);
    newnr = GetCurUserLastNr("VIVc");
    if (newnr==-1) then begin
      newnr = SRRec.LastVINr;      
    end;    
    VIp.SerNr = NextSerNr("VIVc",VIp.TransDate,newnr,false,"");
    if (VIp.SerNr==-1) then begin goto LCreateVIFromVEIV; end;
  end;
  if ((MatRowCnt(VIp)>0) and (VIp.SerNr!=-1)) then begin
    if (RecordStore(VIp,false)) then begin 
      RecordCopy(OldVEIVr,VEIVr);
      VEIVr.VISerNr = VIp.SerNr;
      RecordUpdate(OldVEIVr,VEIVr,false);
      CreateRecordLink(VIp,CurrentCompany,VEIVr,CurrentCompany);  
      CreateRecordLink(VEIVr,CurrentCompany,VIp,CurrentCompany);  
      if (VIp.POSerNr>0) then begin
        POr.SerNr = VIp.POSerNr;
        if (ReadFirstMain(POr,1,true)) then begin
          CreateRecordLink(VIp,CurrentCompany,POr,CurrentCompany);  
          CreateRecordLink(POr,CurrentCompany,VIp,CurrentCompany);  
        end;
      end;
    end;
  end else begin
    res = -1;
  end;
LCreateVIFromVEIV:;  
  CreateVIFromVEIV = res;
  RETURN;
END;

global
function Boolean PastePayRecVI(record VIVc VIp,string VECode)
BEGIN
  record CUVc VEr;
  record CCatVc CCatr;
  record VGVc VGr;
  Boolean found;
 
  VEr.Code = VECode;
  found = ReadFirstMain(VEr,1,true);
  if (found) then begin
    if (GetVATLaw!=5) then begin//vatSwedish
      if (blank(VIp.BankAcc)) then begin
        VIp.BankAcc = VEr.IBANCode;
      end;
      if (blank(VIp.BankAcc)) then begin
        VIp.BankAcc = VEr.BankAccount;
      end;
      if (blank(VIp.BankAcc)) then begin
        VIp.BankAcc = VEr.Bank;
      end;
      if (nonblank(VEr.AccOperator)) then begin
        VIp.BankCode = VEr.AccOperator;
      end;
      if (nonblank(VEr.SortCode)) then begin
        VIp.SortCode = VEr.SortCode;      
      end;
    end;
    if (nonblank(VEr.AccAP)) then begin
      VIp.APAcc = VEr.AccAP;
    end;
    if (nonblank(VEr.CustCat) and blank(VEr.AccAP)) then begin
      
      if (blank(VEr.VECat)) then begin
        CCatr.Code = VEr.CustCat;
        if (ReadFirstMain(CCatr,1,true)) then begin
          if (nonblank(CCatr.AccAP)) then begin
            VIp.APAcc = CCatr.AccAP;
          end;
        end;
      end else begin
        VGr.Code = VEr.VECat;
        if (ReadFirstMain(VGr,1,true)) then begin
          if (nonblank(VGr.AccAP)) then begin
            VIp.APAcc = VGr.AccAP;
          end;
        end;
      end;
    end;
    VICalcEReb(VIp);
  end;
  PastePayRecVI = found;
  RETURN;
END;

